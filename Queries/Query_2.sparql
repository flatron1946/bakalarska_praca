# Vypíš na akom biologickom procese alebo molekulárnej funkcii sa podieľa gén 4140?

# definujeme predpony
PREFIX het: <https://het.io/>
PREFIX gene: <https://bioregistry.io/ncbigene:> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>  

# Dopyt
SELECT 
    ?gene                    # vyberame gen
    ?gene_label              # nazov genu
    ?participates            # URI biologickeho procesu/molekularnej funkcii na ktorej ma ucast gen
    ?process_function_label  # nazov biologickeho procesu/molekularnej funkcie
    ?type                    # typ ucasti - biologicky proces alebo molekularna funkcia
WHERE {
    # definujeme gen
    BIND(gene:4140 as ?gene)
    
    # ziskame nazov genu
    gene:4140 skos:prefLabel ?gene_label .
    
    # zitujeme ci sa gen zucastnuje na biologickom procese
    {
        gene:4140 het:participates ?participates .  
        ?participates rdf:type het:biological_process .     # potvrdime ze sa gen zucastnuje na biologickom procese
        ?participates skos:prefLabel ?process_function_label .     # ziskame nazov biologickeho procesu
        BIND("biological_process" as ?type)     # definujeme typ ucasti ako 'biological_process'
    }
    # zistujeme ci sa gen zucastnuje na molekularnej funkcii
    UNION
    {
        gene:4140 het:participates ?participates .
        ?participates rdf:type het:molecular_function .     # potvrdime ze sa gen zucastnuje na molekularnej funkcii
        ?participates skos:prefLabel ?process_function_label .  # ziskame nazov molekularnej funkcie
        BIND("molecular_function" as ?type)     # definujeme typ ucasti ako 'molecular function'
    }
}
