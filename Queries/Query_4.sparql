# Pre chorobu DOID:9970 nájdi všetky lieky a ich vedľajšie účinky, ktoré nejako súvisia s génmi, ktoré súvisia s touto chorobou. Taktiež vypíš spôsob ako sa spájajú.


# definujeme predpony
PREFIX het: <https://het.io/> 
PREFIX disease: <https://bioregistry.io/DOID:> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>  

# dopyt
SELECT DISTINCT 
    ?diseaseLabel           # nazov choroby
    ?diseaseGeneRelation    # vztah medzi chorobou a genom
    ?geneLabel              # nazov genu
    ?compoundLabel          # nazov zluceniny
    ?compoundGeneRelation   # vztah medzi zluceninou a genom
    # do jednej bunky tabulky cez ciarky vypiseme zoznam vsetkych vedlajsich ucinkov zluceniny
    (GROUP_CONCAT(DISTINCT ?sideEffectLabel; SEPARATOR=", ") AS ?allSideEffects)   
WHERE {
    # definujeme chorobu pre ktoru sa dopytujeme
    BIND(disease:9970 AS ?disease)
    
    # ziskame informacie o vztahu medzi chorobou a genom, a medzi zluceninou a genom
    ?disease ?diseaseGeneRelation ?gene .
    ?compound ?compoundGeneRelation ?gene .

    # overime si, ze zlucenina je typu 'compound' a sposobuje vedlajsi ucinok
    ?compound rdf:type het:compound .
    ?compound het:causes ?sideEffect .

    # odfiltrujeme typy vztahov medzi chorobou a genom, a medzi zluceninou a genom
    FILTER(?diseaseGeneRelation IN (het:associates, het:upregulates, het:downregulates)) 
    FILTER(?compoundGeneRelation IN (het:upregulates, het:downregulates, het:binds))

    # ziskame nazvov choroby, genu, zluceniny a vedlajsich ucinkov
    ?disease skos:prefLabel ?diseaseLabel .
    ?gene skos:prefLabel ?geneLabel .
    ?compound skos:prefLabel ?compoundLabel .
    ?sideEffect skos:prefLabel ?sideEffectLabel .
}
# zoskupime vysledky
GROUP BY ?diseaseLabel ?diseaseGeneRelation ?geneLabel ?compoundLabel ?compoundGeneRelation
